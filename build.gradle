import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'maven-publish'
	id 'eclipse'
	id 'java'
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

repositories {
	mavenCentral()

	// Spigot
	maven {
		url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
	}

	// Bungeecord
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}

	// Skript
	maven {
		url 'https://repo.skriptlang.org/releases'
	}

}

dependencies {

	//Nullable annotation
	implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.2.700')

	//Spigot/Bukkit
	implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.19.4-R0.1-SNAPSHOT')

	//Skript
	implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.7.0-beta2') {
		transitive = false
	}

	shadow group: 'org.bstats', name: 'bstats-bukkit', version: '3.0.1'

	shadow (group: 'org.json', name: 'json', version: '20230227')

}

processResources {
	filter ReplaceTokens, tokens: ["version": project.property("version")]
	from ('lang') {
        include '**/*'
        into 'lang/'
    }
    from ('documentation') {
        into 'documentation/'
    }
}

shadowJar {
	configurations = [project.configurations.shadow]
	dependencies {
		include(dependency('org.bstats:bstats-bukkit'))
		include(dependency('org.bstats:bstats-base'))
	}
	relocate 'org.bstats', 'com.w00tmast3r.skquery.bstats'
	archiveVersion = version
	archiveFileName = project.name + "-" + version + ".jar"
	minimize()
}
